GAL22V10
GLUE

AS   IOSEL   HWRST  FC2    FC1     FC0   A1    A2    A3     A19      NC     GND
NC   DOBOOT  HALT   RESET  RUNLED  BQ0   BQ1   BQ2   BOOT   ANYIACK  DUIACK VCC

HALT.T  =  GND
HALT.E  =  HWRST

RESET.T =  GND
RESET.E =  HWRST

RUNLED  = /HALT

DOBOOT  = HWRST
        + /HALT * /RESET

AR      = DOBOOT
BQ0.R   = /DOBOOT               ; High after one memory cycle
BQ1.R   = /DOBOOT * BQ0         ; High after two memory cycles
BQ2.R   = /DOBOOT * BQ1         ; High after three memory cycles
BOOT.R  = /DOBOOT * BQ2         ; High after four memory cycles

/ANYIACK= /HWRST * FC0 * FC1 * FC2 * A19
/DUIACK = /HWRST * FC0 * FC1 * FC2 * /AS * /A1 * /A2 * A3 * A19

DESCRIPTION

rosco_m68k glue for Classic board 2.0

Copyright (c)2022 Ross Bamford & Contributors
See LICENSE.md for licence details (spoiler: CERN / MIT).

This IC provides general reset and glue logic for rosco_m68k 
classic 2.0. It is broadly based on the reset glue from the pro.

