GAL16V8
GLUE

FC0   FC1   FC2   ODDROM    ODDRAM    EVENROM   EVENRAM   IOSEL   IODTACK   GND
A17   NC    IACK  PPDTACK   MFPDS     RAMOE     RW        LDS     DTACK     VCC

/IACK     = A17 * FC0 * FC1 * FC2
/PPDTACK  = /IOSEL * /IODTACK
          + /IACK * /IODTACK
          + IACK * /EVENROM
          + IACK * /ODDROM
          + IACK * /EVENRAM
          + IACK * /ODDRAM
          + IACK * ODDROM * EVENROM * ODDRAM * EVENRAM * IOSEL

/MFPDS    = /LDS 
          + /IACK

/RAMOE    = RW

DTACK.T   = GND
DTACK.E   = /PPDTACK

DESCRIPTION

IC5 (DTACK and MFP Glue Logic for rosco_m68k)
 
Copyright (c)2020 Ross Bamford. See LICENSE.

